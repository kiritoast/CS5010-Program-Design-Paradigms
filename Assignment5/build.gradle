plugins {
// Build Java: https://docs.gradle.org/current/userguide/building_java_projects.html
    id 'java'
// https://docs.gradle.org/current/userguide/pmd_plugin.html#header
    id 'pmd'
// https://docs.gradle.org/current/userguide/jacoco_plugin.html
    id 'jacoco'
    id "application"
}

group 'cs5010seaS23'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    implementation 'com.opencsv:opencsv:5.7.1'
}

pmd {
    ignoreFailures=true
    pmdTest.enabled=false
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/errorprone.xml",
            "category/java/codestyle.xml"
//"java-basic",
//"java-braces",
//"java-strings",
    ]
}

javadoc {
    doLast {
        String fixedBuildDir = buildDir.toString().replace('\\', '/')
        println "file:///$fixedBuildDir/docs/javadoc/index.html"
    }
}
jacoco {
    toolVersion = "0.8.7"
}
jacocoTestReport {
    reports {
        html.destination file("${buildDir}/jacocoHtml")
    }
    doLast {
        String fixedBuildDir = buildDir.toString().replace('\\', '/')
        println "file:///$fixedBuildDir/jacocoHtml/index.html"
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
// minimum percentage of code coverage
                minimum = 0.7
            }
        }
        rule {
            enabled = false
            element = 'CLASS'
            includes = ['org.gradle.*']
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }
}

// Fail the build if code coverage isn't high enough
check.dependsOn jacocoTestCoverageVerification
// Run code coverage after tests run
jacocoTestReport.mustRunAfter test

ext {
    javaMainClass = "assignment4.problem1.SentenceGeneratorApp"
}

application {
    mainClassName = javaMainClass
}

run {
    standardInput = System.in
}

task doAll {
// Runs a java build. Imported from the java plugin
    dependsOn build
// Runs all the tests. Imported from the java plugin.
    dependsOn test
// Runs pmd. Imported by pmd plugin
    dependsOn check
// Runs java doc. Imported from the java plugin
    dependsOn javadoc
    println 'all done!'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}